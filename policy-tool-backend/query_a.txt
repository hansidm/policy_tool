PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT distinct ?attribute ?property ?range ?propertyType ?extent ?cardinality WHERE {
  ?attribute rdfs:subClassOf sio:Attribute.
  ?attribute (rdfs:subClassOf|owl:equivalentClass|(owl:intersectionOf/rdf:rest*/rdf:first))* ?superClass.
  {
    ?superClass owl:onProperty ?property.
    ?superClass owl:someValuesFrom|owl:allValuesFrom ?range.
    ?superClass ?extent ?range.
    optional { ?property rdf:type ?propertyType }
  } union {
    ?superClass owl:onDataRange ?range.
    ?superClass owl:onProperty ?property.
    ?superClass owl:minQualifiedCardinality|owl:maxQualifiedCardinality|owl:qualifiedCardinality ?cardinality.
    ?superClass ?extent ?cardinality.
    bind(owl:DatatypeProperty as ?propertyType)
  } union {
    ?superClass owl:onClass ?range.
    ?superClass owl:onProperty ?property.
    ?superClass owl:minQualifiedCardinality|owl:maxQualifiedCardinality|owl:qualifiedCardinality ?cardinality.
    ?superClass ?extent ?cardinality.
    bind(owl:ObjectProperty as ?propertyType)
  } union {
    ?superClass owl:onProperty ?property.
    ?superClass owl:minCardinality|owl:maxCardinality|owl:exactCardinality ?cardinality.
    ?superClass ?extent ?cardinality.
    optional { ?property rdf:type ?propertyType }
  } union {
    ?property rdfs:domain ?superClass;
              rdfs:range ?range.
    optional { ?property rdf:type ?propertyType }
  }
}